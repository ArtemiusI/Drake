// BLOCKING UNAPPROVED "CROSSMOD"

IF
  !Global("SanMeetC0Drake","Global",999)
THEN
  RESPONSE #100
    SetGlobal("SanMeetC0Drake","GLOBAL",999)
END

IF
AreaCheck("BG3700")
!Global("SanDrakeChic","BG3700",999)
THEN
RESPONSE #100
SetGlobal("SanDrakeChic","BG3700",999)
END

IF
OR(2)
Global("C0DrakeBassilus","GLOBAL",4)
Global("C0DrakeBassilus","GLOBAL",5)
THEN
RESPONSE #100
SetGlobal("C0DrakeBassilus","GLOBAL",999)
END

IF
!Global("SanMeetC0Drake","Global",999)
THEN
RESPONSE #100
SetGlobal("SanMeetC0Drake","Global",999)
END

// EXPERIENCE

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,31999)
  XPLT(Myself,32000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,32000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,15999)
  XPLT(Myself,16000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,16000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,7999)
  XPLT(Myself,8000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,8000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,3999)
  XPLT(Myself,4000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,4000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,1999)
  XPLT(Myself,2000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,2000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPLT(Player1,1000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    SetInterrupt(TRUE)
END

// FRIENDSHIP

// bassilus must be dead

IF
    InParty(Myself)
    Dead("bassilus")
    ReputationGT(Player1,9)
    Global("C0DrakeTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C0DrakeTalkTimer","GLOBAL",300)
    SetGlobal("C0DrakeTalk","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0DrakeTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	!Global("C0DrakeTalkStopped","GLOBAL",1)
    OR(11)
      Global("C0DrakeTalk","GLOBAL",2)
      Global("C0DrakeTalk","GLOBAL",4)
      Global("C0DrakeTalk","GLOBAL",6)
      Global("C0DrakeTalk","GLOBAL",8)
      Global("C0DrakeTalk","GLOBAL",12)
      Global("C0DrakeTalk","GLOBAL",14)
	  Global("C0DrakeTalk","GLOBAL",16)
	  Global("C0DrakeTalk","GLOBAL",22)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0DrakeTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
    OR(11)
      Global("C0DrakeTalk","GLOBAL",1)
	  Global("C0DrakeTalk","GLOBAL",3)
      Global("C0DrakeTalk","GLOBAL",5)
      Global("C0DrakeTalk","GLOBAL",7)
      Global("C0DrakeTalk","GLOBAL",11)
      Global("C0DrakeTalk","GLOBAL",13)
	  Global("C0DrakeTalk","GLOBAL",15)
	  Global("C0DrakeTalk","GLOBAL",17)
	  Global("C0DrakeTalk","GLOBAL",21)
THEN
  RESPONSE #100
	IncrementGlobal("C0DrakeTalk","GLOBAL",1)
END

// TALK 10 - post Mines

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0DrakeTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	GlobalGT("Chapter","GLOBAL",%tutu_chapter_4%)
	Global("C0DrakeTalk","GLOBAL",18)
	!Global("C0DrakeTalkStopped","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

// TALK 10 - after Stoneheart quest

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0DrakeTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	Global("C0DrakeStoneheart","GLOBAL",5)
	!Global("C0DrakeTalkStopped","GLOBAL",1)
	Global("C0DrakeTalk","GLOBAL",20)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0DrakeTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	Global("C0DrakeStoneheart","GLOBAL",5)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
    Global("C0DrakeTalk","GLOBAL",19)
THEN
  RESPONSE #100
	IncrementGlobal("C0DrakeTalk","GLOBAL",1)
END

// TALK 12 - Gorion's letter

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
	PartyHasItem("scrl2j")
    ReputationGT(Player1,9)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
	Global("C0DrakeBhaalspawnTalk","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(4)
    SetGlobal("C0DrakeBhaalspawnTalk","GLOBAL",1)
	StartDialogueNoSet(Player1)
END


// TALK 13 - Final, Undercity

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
	AreaCheck("%Undercity%")
    ReputationGT(Player1,9)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
	Global("C0DrakeTalk","GLOBAL",23)
THEN
  RESPONSE #100
    SetGlobal("C0DrakeTalk","GLOBAL",24)
	StartDialogueNoSet(Player1)
END

// Enable flirting

IF
	InParty(Myself)
	!Alignment(Player1,MASK_EVIL)
	CheckStatGT(Player1,11,CHR)
	Global("C0DrakeMatch","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("C0DrakeMatch","GLOBAL",1)
END

// INTERJECTIONS

// Bassilus

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0DrakeBassilus","GLOBAL",1)
	Dead("bassilus")
	CombatCounter(0)
	!See([ENEMY])
THEN
  RESPONSE #100
	SetGlobal("C0DrakeBassilus","GLOBAL",2)
	StartDialogueNoSet(Player1)
END

// Pantaloons

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	PartyHasItem("misc47")
	Global("C0DrakePantaloons","GLOBAL",0)
	AreaCheck("%FriendlyArmInn_L3%")
	CombatCounter(0)
	!See([ENEMY])
THEN
  RESPONSE #100
	SetGlobal("C0DrakePantaloons","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Nashkel town

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("%Nashkel%")
	Global("C0DrakeNashkel","GLOBAL",0)
	CombatCounter(0)
	!See([ENEMY])
THEN
  RESPONSE #100
	SetGlobal("C0DrakeNashkel","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Nashkel carnival

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("%NashkelCarnival%")
	See("gazib")
	Global("C0DrakeCarnival","GLOBAL",0)
	CombatCounter(0)
	!See([ENEMY])
THEN
  RESPONSE #100
	SetGlobal("C0DrakeCarnival","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Petrine's Cat

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Dead("PetrinesCat")
	Global("C0DrakePetrineCat","GLOBAL",0)
	CombatCounter(0)
	!See([ENEMY])
THEN
  RESPONSE #100
	SetGlobal("C0DrakePetrineCat","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Stoneheart

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!See("c0dstone")
	Global("C0DrakeStoneheart","GLOBAL",1)
	Global("C0DrakeStoneheartTalk","GLOBAL",0)
	CombatCounter(0)
	!See([ENEMY])
THEN
  RESPONSE #100
	SetGlobal("C0DrakeStoneheartTalk","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Undercellar

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("%BaldursGate_Undercellars%")
	Global("C0DrakeUndercellar","GLOBAL",0)
	CombatCounter(0)
	!See([ENEMY])
THEN
  RESPONSE #100
	SetGlobal("C0DrakeUndercellar","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Angelo

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0DrakeAngelo","GLOBAL",1)
	CombatCounter(0)
	!See([ENEMY])
THEN
  RESPONSE #100
	SetGlobal("C0DrakeAngelo","GLOBAL",2)
	StartDialogueNoSet(Player1)
END

// Delainy/Durlyle

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0DrakeWolfFlirt","GLOBAL",0)
	Global("WillHelpEscape","GLOBAL",1)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
THEN
  RESPONSE #100
	SetGlobal("C0DrakeWolfFlirt","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0DrakeHelmAlignment","GLOBAL",0)
	CombatCounter(0)
    AreaCheck("%DurlagsTower_DemonknightsChamber%")
	Dead("DEATHK1")
	PartyHasItem("helm02")
	!See([ENEMY])
	See(Player1)
THEN
  RESPONSE #100
	SetGlobal("C0DrakeHelmAlignment","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Killed Aec'letec

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0DrakeAecBanished","GLOBAL",0)
	CombatCounter(0)
    AreaType([OUTDOOR])	
	Dead("TANAR")
	!See([ENEMY])
	See(Player1)
THEN
  RESPONSE #100
	SetGlobal("C0DrakeAecBanished","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// low rep

IF
	InParty(Myself)
    !ReputationGT(Player1,9)
	Global("C0DrakeLowRep","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("C0DrakeLowRep","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
    !ReputationGT(Player1,5)
	Global("C0DrakeRepBreak","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("C0DrakeRepBreak","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
    ReputationGT(Player1,9)
	Global("C0DrakeLowRep","GLOBAL",1)
	Global("C0DrakeRepBreak","GLOBAL",1)
THEN
  RESPONSE #100
	SetGlobal("C0DrakeLowRep","GLOBAL",0)
	SetGlobal("C0DrakeRepBreak","GLOBAL",0)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
    !ReputationGT(Player1,9)
	Global("C0DrakeLowRep","GLOBAL",1)
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
    !ReputationGT(Player1,5)
	Global("C0DrakeRepBreak","GLOBAL",1)
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END

// Drunk

IF
Global("C0DrakeDrunkTalk","GLOBAL",0)
InParty(Myself)
CheckStatGT(Myself,30,INTOXICATION)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("C0DrakeDrunkTalk","GLOBAL",1)
StartDialogueNoSet(Player1)
END

IF
Global("C0DrakeDrunkTalk","GLOBAL",1)
InParty(Myself)
!CheckStatGT(Myself,30,INTOXICATION)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("C0DrakeDrunkTalk","GLOBAL",2)
StartDialogueNoSet(Player1)
END

IF
Global("C0DrakeDrunkTalk","GLOBAL",3)
InParty(Myself)
CheckStatGT(Myself,30,INTOXICATION)
GlobalGT("Chapter","GLOBAL",%tutu_chapter_4%)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("C0DrakeDrunkTalk","GLOBAL",4)
StartDialogueNoSet(Player1)
END

// SEX CHANGE

IF
    InParty(Myself)
	HasItemEquiped("belt05",Myself)
    Global("C0DrakeSexChange","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("C0DrakeSexChange","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Frozen Aegis

IF
	HasItemEquipedReal("c0dhamm",Myself)
	See([ENEMY])
	Global("ColdfaithAegis","LOCALS",0)
THEN
RESPONSE #100
	SetGlobal("ColdfaithAegis","LOCALS",1)
	ApplySpellRES("c0dhamm4",Myself)
END

IF
	Global("ColdfaithAegis","LOCALS",1)
	OR(2)
	!HasItemEquipedReal("c0dhamm",Myself)
	!See([ENEMY])
THEN
RESPONSE #100
	SetGlobal("ColdfaithAegis","LOCALS",0)
	ApplySpellRES("c0dhamm5",Myself)
END