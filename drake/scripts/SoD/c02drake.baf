// Introduction

IF
	!InParty(Myself)
	AreaCheck("bd0106")
	See(Player1)
	Global("C02DrakeBegin","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(2)
	SetGlobal("C02DrakeBegin","GLOBAL",1)
	StartDialogueNoSet(Player1)
	Continue()
END

// EXPERIENCE

IF
  Global("BD_JOINXP","LOCALS",1)
  Global("BD_SAFEHOUSE_DONE","GLOBAL",1)
  Global("BDSODXP","LOCALS",0)
  InParty(Myself)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BDSODXP","LOCALS",1)
    SetGlobal("BD_JOINXP","LOCALS",0)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,249999)
  XPLT(Myself,250000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,250000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,199999)
  XPLT(Myself,200000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,200000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,160999)
  XPLT(Myself,161000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,161000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,134999)
  XPLT(Player1,161000)
  XPLT(Myself,135000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,135000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,109999)
  XPLT(Player1,135000)
  XPLT(Myself,110000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,110000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,89999)
  XPLT(Player1,110000)
  XPLT(Myself,90000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,90000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPLT(Player1,90000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    SetInterrupt(TRUE)
END

// PLAYER TALKS

IF
    InParty(Myself)
    Global("C02DrakeTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C02DrakeTalkTimer","GLOBAL",300)
    SetGlobal("C02DrakeTalk","GLOBAL",1)
END

IF
    InParty(Myself)
    Global("C0DrakeMatch","GLOBAL",0)
	CheckStatGT(Player1,15,CHR)
	!Alignment(Player1,MASK_EVIL)
THEN
  RESPONSE #100
	SetGlobal("C0DrakeMatch","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C02DrakeTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	!Global("C0DrakeTalkStopped","GLOBAL",1)
    OR(5)
      Global("C02DrakeTalk","GLOBAL",2)
      Global("C02DrakeTalk","GLOBAL",6)
      Global("C02DrakeTalk","GLOBAL",8)
      Global("C02DrakeTalk","GLOBAL",10)
	  Global("C02DrakeTalk","GLOBAL",14)
THEN
  RESPONSE #100
	RealSetGlobalTimer("C02DrakeTalkTimer","GLOBAL",THIRTY_MINUTES)
    StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C02DrakeTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
    OR(4)
      Global("C02DrakeTalk","GLOBAL",1)
      Global("C02DrakeTalk","GLOBAL",7)
      Global("C02DrakeTalk","GLOBAL",9)
	  Global("C02DrakeTalk","GLOBAL",13)
THEN
  RESPONSE #100
	IncrementGlobal("C02DrakeTalk","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C02DrakeTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	Global("bd_dragon_ot","bd7210",1)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
      Global("C02DrakeTalk","GLOBAL",5)
THEN
  RESPONSE #100
	IncrementGlobal("C02DrakeTalk","GLOBAL",1)
END

IF
    InParty(Myself)
	AreaCheck("bd4400")
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
    !Detect([ENEMY])
	Global("C02DrakeHellTalk","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("C02DrakeHellTalk","GLOBAL",1)
	RealSetGlobalTimer("C02DrakeHellTalkTimer","GLOBAL",5)
END

IF
    InParty(Myself)
	AreaCheck("bd4400")
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
    !Detect([ENEMY])
	Global("C02DrakeHellTalk","GLOBAL",1)
	RealGlobalTimerExpired("C02DrakeHellTalkTimer","GLOBAL")
THEN
  RESPONSE #100
	SetGlobal("C02DrakeHellTalk","GLOBAL",2)
	StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
	GlobalGT("bd_plot","global",590)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
    !Detect([ENEMY])
    ReputationGT(Player1,9)
	Global("C02DrakeLastTalk","GLOBAL",0)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
THEN
  RESPONSE #100
	SetGlobal("C02DrakeLastTalk","GLOBAL",1)
	RealSetGlobalTimer("C02DrakeLastTalkTimer","GLOBAL",5)
END

IF
    InParty(Myself)
	GlobalGT("bd_plot","global",590)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
    !Detect([ENEMY])
	Global("C02DrakeHellTalk","GLOBAL",1)
	RealGlobalTimerExpired("C02DrakeLastTalkTimer","GLOBAL")
THEN
  RESPONSE #100
	SetGlobal("C02DrakeHellTalk","GLOBAL",2)
	StartDialogueNoSet(Player1)
END

// Romantic Talks

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	Global("C0DrakeMatch","GLOBAL",1)
	GlobalGT("C02DrakeFlirt","GLOBAL",1)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
	Global("C02DrakeRomance","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("C02DrakeRomance","GLOBAL",1)
	RealSetGlobalTimer("C02DrakeRomanticTalkTimer","GLOBAL",THIRTY_MINUTES)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C02DrakeRomanticTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	Global("C0DrakeMatch","GLOBAL",1)
	!Global("C0DrakeTalkStopped","GLOBAL",1)
		Global("C02DrakeRomanticTalk","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
	Dead("bdashati")
	Global("C0DrakeMatch","GLOBAL",1)
	!Global("C0DrakeTalkStopped","GLOBAL",1)
		Global("C02DrakeRomanticTalk","GLOBAL",3)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	Global("C0DrakeMatch","GLOBAL",1)
	GlobalGT("C02DrakeFlirt","GLOBAL",1)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
    OR(2)
		Global("C02DrakeRomanticTalk","GLOBAL",0)
		Global("C02DrakeRomanticTalk","GLOBAL",2)
THEN
  RESPONSE #100
	IncrementGlobal("C02DrakeRomanticTalk","GLOBAL",1)
END

IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !Detect([ENEMY])
    ReputationGT(Player1,9)
	GlobalGT("bd_plot","global",480)
    Global("C02DrakeRomanceActive","GLOBAL",2)
    Global("C02DrakeLastRomanticTalk","GLOBAL",0)
    !Global("C0DrakeTalkStopped","GLOBAL",1)
THEN
  RESPONSE #100
	SetGlobal("C02DrakeLastRomanticTalk","GLOBAL",1)
END

IF
	Global("C02DrakeLastRomanticTalk","GLOBAL",1)
	PartyHasItem("c0dlock")
	!HasItem("c0dlock",Myself)
THEN
  RESPONSE #100
	TakePartyItem("c0dlock")
END

IF
	Global("C02DrakeLastRomanticTalk","GLOBAL",1)
	!PartyHasItem("c0dlock")
	!HasItem("c0dlock",Myself)
THEN
  RESPONSE #100
	GiveItemCreate("c0dlock",Myself,1,0,0)
END

IF
	Global("C02DrakeLastRomanticTalk","GLOBAL",1)
	HasItem("c0dlock",Myself)
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END


// Morenthene

IF
    InParty(Myself)
    See("BDMORENT")
	!Dead("BDMORENT")
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !Detect([ENEMY])
    Global("C02DrakeMorenthene","LOCALS",0)
THEN
  RESPONSE #100
	SetGlobal("C02DrakeMorenthene","LOCALS",1)
	DisplayStringHead(Myself,~Well bless my unworthy soul... now THIS is an incredible sight.~)
END

// Burned Inn

IF
    InParty(Myself)
    See("GentrusPanel")
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !Detect([ENEMY])
    Global("C02DrakeInnTalk","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("C02DrakeInnTalk","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Tender of the Dead

IF
	InParty(Myself)
	Dead("BDYMORI")
	AreaCheck("BD2000")
    CombatCounter(0)
	Global("C02DrakeVelsharoon","GLOBAL",0)
	Global("KNOW_VELSHAROON","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("C02DrakeVelsharoon","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Kicked out in Avernus

IF
  !InParty(Myself)
  Global("bd_joined","LOCALS",1)
  AreaCheck("bd4700")
  GlobalLT("bd_plot","global",570)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

// Overhead Interjections

IF
    InParty("C0Drake")
    Global("C02DrakeCryptComment","bd1020",0)
	Global("BDSH_Imoen_Floor","GLOBAL",3)
	Global("BDSH_Rope","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("C02DrakeCryptComment","bd1020",1)
    RealSetGlobalTimer("C02DrakeCryptCommentTimer","bd1020",5)
END

IF
    InParty("C0Drake")
    !StateCheck("C0Drake",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !Detect([ENEMY])
    RealGlobalTimerExpired("C02DrakeCryptCommentTimer","bd1020")
    Global("C02DrakeCryptComment","bd1020",1)
THEN
  RESPONSE #100
    SetGlobal("C02DrakeCryptComment","bd1020",2)
	DisplayStringHead(Myself,~A job well done, if I do say so myself. What say we head to the nearest tavern and crack some of their finest kegs of ale once we are through with this sad place?~)
END

IF
  Global("bd_MDD892a_ot","bd4300",0)
  Global("bd_poison_dsc_supplies","global",0)
  Dead("bdbelben")
  CombatCounter(0)
  IfValidForPartyDialog("C0Drake")
THEN
  RESPONSE #100
    SetGlobal("bd_MDD892a_ot","bd4300",1)
    SetGlobalTimer("bd_MDD892a_ot_timer","bd4300",3)
    DisplayStringHead("C0Drake",~I'd say we did the crusade a favor putting that one to rest for good.~)
END

IF
  Global("bd_plot","global",370)
  !GlobalTimerNotExpired("bd_mdd905a_ot_timer","bd4300")
  Global("bd_ot_C0Drake","bd4300",0)
  IfValidForPartyDialog("C0Drake")
THEN
  RESPONSE #100
    SetGlobalTimer("bd_mdd905a_ot_timer","bd4300",THREE_MINUTES)
    SetGlobal("bd_ot_C0Drake","bd4300",1)
    DisplayStringHead("C0Drake",~As entertaining as this may be, the odds are hardly in our favor. Might I suggest a tactical retreat? As in, right NOW?~)
END

IF
  Global("bd_mdd1290d_ot","bd4300",1)
  Global("bd_mdd1290d_ot_C0Drake","bd4300",0)
  InParty("C0Drake")
THEN
  RESPONSE #100
    DisplayStringHead("C0Drake",~We'll be at this all day, <CHARNAME>. As bad of an idea as it sounds, I suggest we take a dive!~)
    SetGlobal("bd_mdd1290d_ot","bd4300",2)
    SetGlobal("bd_mdd1290d_ot_C0Drake","bd4300",1)
END

IF
  Global("bd_hellevator_ot_C0Drake","bd4601",0)
  InParty("C0Drake")
  !GlobalTimerNotExpired("bd_hellevator_timer","bd4601")
  !ActuallyInCombat()
THEN
  RESPONSE #100
    SetGlobal("bd_hellevator_ot_C0Drake","bd4601",1)
    SetGlobalTimer("bd_hellevator_timer","bd4601",7)
    DisplayStringHead("C0Drake",~Up and up we go, where we'll end up, nobody knows... heh. I'd suggest preparing for the worst, whatever that may be.~)
END

IF
  Global("bd_plot","global",586)
  !GlobalTimerNotExpired("bd_mdd1341a_ot_timer","bd4300")
  Global("bd_ot2_C0Drake","bd4300",0)
  InParty("C0Drake")
THEN
  RESPONSE #100
    SetGlobalTimer("bd_mdd1341a_ot_timer","bd4300",4)
    SetGlobal("bd_ot2_C0Drake","bd4300",1)
    DisplayStringHead("C0Drake",~That'll make a tale worthy of my fellows of the Order. I never thought I'd be alive to tell it.~)
END

// QUEST

IF
  AreaCheck("BD3000")
  See([PC])
  !ActuallyInCombat()
  !See([ENEMY])
  GlobalGT("bd_plot","global",304)
  GlobalLT("bd_plot","global",350)
  Global("C02DrakeQuest","GLOBAL",0)
THEN
  RESPONSE #100
	RealSetGlobalTimer("C02DrakeQuestTimer","bd3000",ONE_MINUTE)
    SetGlobal("C02DrakeQuest","GLOBAL",1)
END

IF
  AreaCheck("BD3000")
  See([PC])
  !ActuallyInCombat()
  !See([ENEMY])
  GlobalGT("bd_plot","global",304)
  GlobalLT("bd_plot","global",350)
  RealGlobalTimerExpired("C02DrakeQuestTimer","bd3000")
  Global("C02DrakeQuest","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("C02DrakeQuest","GLOBAL",2)
	StartDialogueNoSet([PC])
END